local ATTR_FULLNAME = "VQl0wK3eqiw";
local ATTR_PHONE = "gGAQeOr1Pgu";
local ATTR_DOB = "Yie7mOY913J";
local ATTR_GENDER = "p7zizFkC6Lv";
local ATTR_REGNO = "CSZevH4P5yV";
local ATTR_NIC = "M6NNPC3hNrb";
local ATTR_ADDRESS = "EOMGwaUTMrU";
local ATTR_PHN = "IrUmPkFMDU5";

local getAttrById(attr) = ds.filter(body.enrollments[0].attributes, function(v, i) v.attribute == attr)[0].value default null;
local parseFullName() = 
  local fullNameAttr = getAttrById("VQl0wK3eqiw");
  if fullNameAttr == null then []
  else
    local fullNameSplit = std.split(fullNameAttr," ");
    local names = [name for name in fullNameSplit if name!= ""];
    local n = std.length(names);
    if n == 0 then []
    else
      [{
        text: fullNameAttr,
        given: if n > 1 then [names[i] for i in std.range(0, n - 2)] else [],
        family: names[n-1]
      }];

local parseAddress(address) =
  local addressAttr = std.map(ds.trim, std.split(address, ","));
  local postalCity = std.map(ds.trim,std.split(addressAttr[1], " "));
  {
    line: [ addressAttr[0] ],
    postalCode: postalCity[0],
    city: postalCity[1],
    district: addressAttr[2],
    state: addressAttr[3],
    country: if std.length(addressAttr) > 4 then addressAttr[4] else "LK"
  };
{
  resourceType: "Bundle",
  type: "transaction",
  entry: [
    {
      fullUrl: "urn:uuid:ff991a8e-db1c-dfe2-e5b1-e411451eb602",
      resource: {
        resourceType: "Patient",
        meta: [
          {
            profile: "http://fhir.health.gov.lk/ips/StructureDefinition/ips-patient"
          }
        ],
        extension: [
          {
            url: "http://fhir.health.gov.lk/ips/StructureDefinition/patient-registration-system",
            valueReference: {
              reference: "Device?identifier=http://fhir.health.gov.lk/ips/identifier/system-id|5b21b377-f424-48c1-8c24-1980b4d00059"
            }
          }
        ],
        identifier: [
          {
            use: "official",
            type: {
              coding: [
                {
                  system: "http://fhir.health.gov.lk/ips/CodeSystem/cs-identifier-types",
                  code: "PHN",
                  display: "Personal Health Number"
                }
              ],
              text: "Personal Health Number"
            },
            system: "http://fhir.health.gov.lk/ips/identifier/phn",
            value: getAttrById(ATTR_PHN)
          },
          {
            use: "secondary",
            type: {
              coding: [
                {
                  system: "http://fhir.health.gov.lk/ips/CodeSystem/cs-identifier-types",
                  code: "NIC",
                  display: "National Identity Card"
                }
              ],
              text: "National identity number"
            },
            system: "http://fhir.health.gov.lk/ips/identifier/nic",
            value: getAttrById(ATTR_NIC)
          },
          {
            use: "secondary",
            system: "urn:dhis2:anc:regno",
            value: getAttrById(ATTR_REGNO)
          }
        ],
        name: parseFullName(),
        telecom: [
          {
            system: "phone",
            value: getAttrById(ATTR_PHONE)
          }
        ],
        gender: ds.lower(getAttrById(ATTR_GENDER)),
        birthDate: getAttrById(ATTR_DOB),
        address: [
          parseAddress(getAttrById(ATTR_ADDRESS))
        ],
      },
      request: {
        method: "PUT",
        url: "Patient?identifier=http://fhir.health.gov.lk/ips/identifier/phn|" + getAttrById(ATTR_PHN)
      }
    }
  ]
}
